import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        buildConfig = [
            'compileSdk': 34,
            'minSdk'    : 24,
            'targetSdk' : 34,
        ]
    }
}

plugins {
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.kapt) apply false
    alias(libs.plugins.kotlin.parcelize) apply false
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.apollo) apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    tasks.withType(Test) {
        useJUnitPlatform()
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
            freeCompilerArgs += "-opt-in=kotlin.time.ExperimentalTime"

            jvmTarget = JavaVersion.VERSION_11
        }
    }

    configurations.configureEach {
        exclude group: 'androidx.appcompat'
        exclude group: 'com.google.android.material', module: 'material'

        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def group = details.requested.group
            def module = details.requested.module.name

            if (group == 'org.jetbrains.kotlin' && module.startsWith('kotlin-stdlib')) {
                details.useVersion versionFor(project, "version.kotlin")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.layout.buildDirectory
}
